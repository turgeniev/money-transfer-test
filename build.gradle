plugins {
    id 'java'
    id 'jacoco'
    id 'application'
}

jacoco {
    toolVersion = "0.8.2"
}

group 'com.revolut'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    final def jerseyVer = '2.29.1'
    implementation "org.glassfish.jersey.containers:jersey-container-jetty-http:${jerseyVer}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVer}"
    implementation "org.glassfish.jersey.inject:jersey-hk2:${jerseyVer}"
    // JAX-B dependencies for JDK 9+
    final def jaxbVer = '2.2.11'
    implementation "javax.xml.bind:jaxb-api:${jaxbVer}"
    implementation "com.sun.xml.bind:jaxb-core:${jaxbVer}"
    implementation "com.sun.xml.bind:jaxb-impl:${jaxbVer}"
    implementation "javax.activation:activation:1.1.1"

    testImplementation "org.glassfish.jersey.core:jersey-client:${jerseyVer}"

    final def junit5ver = '5.1.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5ver}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5ver}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit5ver}"
}

application {
    mainClassName = "com.revolut.account.Application"
}

// ======= tests
test {
    // enable JUnit5 support, see https://junit.org/junit5/docs/current/user-guide/#running-tests-build-gradle
    useJUnitPlatform()
}

// ======= test coverage
jacocoTestReport {
    reports {
        html.enabled true
    }
}

// coverage task, dependencies: coverage -> jacocoTestReport -> test
jacocoTestReport.dependsOn test
task coverage(dependsOn: jacocoTestReport) {
    description 'Runs tests and generates coverage report in ./build/reports/jacoco'
}